apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("keystore.properties")

android {
    // Signing
    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            config {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    // Android
    compileSdkVersion 27
    defaultConfig {
        applicationId "de.winterrettich.ninaradio"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "de.winterrettich.ninaradio.MockApplicationRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.config
            }
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    // Java
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Prevent duplicate file
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    // Design
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    implementation 'com.github.ivbaranov:materialfavoritebutton:0.1.4'
    // ORM
    implementation 'com.github.vicpinm:activeandroidrx:3.1.5'
    // Communication
    // TODO switch to RxAndroid?
    implementation 'com.squareup:otto:1.3.8'
    // Webservice requests
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // JUnit + Hamcrest
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support:support-annotations:27.0.2'
    androidTestImplementation 'org.hamcrest:hamcrest-core:1.3'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // UI Tests
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    // Mocking
    androidTestImplementation 'org.mockito:mockito-core:2.8.47'
    androidTestImplementation 'org.mockito:mockito-android:2.8.47'
    androidTestImplementation 'com.squareup.retrofit2:retrofit-mock:2.3.0'
    androidTestImplementation 'com.squareup.okhttp3:okhttp:3.8.1'
}

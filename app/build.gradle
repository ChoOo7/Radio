apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    // Signing
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    // Android
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "de.winterrettich.ninaradio"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'de.winterrettich.ninaradio.MockApplicationRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    // Java
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Prevent duplicate file
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    // Design
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    compile 'com.github.ivbaranov:materialfavoritebutton:0.1.4'
    // ORM
    compile 'com.github.vicpinm:activeandroidrx:3.1.5'
    // Communication
    compile 'com.squareup:otto:1.3.8' // TODO switch to RxAndroid?
    // Webservice requests
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // JUnit + Hamcrest
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    // UI Tests
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    // Mocking
    androidTestCompile 'org.mockito:mockito-core:2.8.47'
    androidTestCompile 'org.mockito:mockito-android:2.8.47'
    androidTestCompile 'com.squareup.retrofit2:retrofit-mock:2.3.0'
    androidTestCompile 'com.squareup.okhttp3:okhttp:3.8.1'
}
